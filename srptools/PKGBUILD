# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs
pkgname=srptools
pkgver=1.0.1
pkgrel=2
pkgdesc="Discover and use SCSI devices via the SCSI RDMA Protocol over InfiniBand."
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('libibumad' 'libibverbs')
source=("https://www.openfabrics.org/downloads/srptools/${pkgname}-${pkgver}.tar.gz")
md5sums=('dfec73fda72bc2e2c234c1cffe62a190')
sha512sums=('24fd7497f89102fdc4ab7183258464ad3fb60dbc5d316b69b04020c38f6a7cd7eee6e023b697a3c74c305f263bfec545192521a5c591df5abfac15e8ebf60402')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --sbindir=/usr/bin \
              --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  # Remove init.d
  rm -rf "${pkgdir}/etc/init.d"

  # TODO: Add systemd service file for srpd
}
