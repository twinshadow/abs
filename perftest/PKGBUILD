# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs
pkgname=perftest
pkgver=2.0
_subver=0.75
_commit=g636d19b
pkgrel=4
pkgdesc="InfiniBand performance tests"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('bash' 'libibumad' 'librdmacm')
source=("http://www.openfabrics.org/downloads/${pkgname}/${pkgname}-${pkgver}-${_subver}.${_commit}.tar.gz")
# cksum start
md5sums=('21b2afdce76aa66b12b55d95937480da')
sha512sums=('3378218ba6dc00bb1842fdcaaaa9dd87f5b5ba1c4cc4fcaaf2ecb6b58a82c2898ff5b505f11bf20a4ef960028ee9509bf9e51fba274f0daadcd4ac76df5963f7')
# cksum end

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p "${pkgdir}/usr/bin"
  install -Dm755 ib_* "${pkgdir}/usr/bin"
  install -Dm755 runme "${pkgdir}/usr/share/${pkgname}/runme"
  install -Dm644 README "${pkgdir}/usr/share/${pkgname}/README"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
