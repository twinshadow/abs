# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs
pkgname=libipathverbs
pkgver=1.2
pkgrel=2
pkgdesc="Userspace driver for QLogic HCAs"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('libibverbs')
source=("http://www.openfabrics.org/downloads/${pkgname}/${pkgname}-${pkgver}.tar.gz")
# cksum start
md5sums=('5fc6b28891b76129d9146683c24091ad')
sha512sums=('222eee073c651da4e51ceea77ae5efcee53cedf07d05e72303ef16d7cf1c7cf199272d016272e2b36ee1a46ddddd2ca8879f801de4dc14851c7ff930631a62f5')
# cksum end

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
