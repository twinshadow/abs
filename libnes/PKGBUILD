# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs
pkgname=libnes
pkgver=1.1.4
pkgrel=1
pkgdesc="Userspace driver for NetEffect Ethernet Cluster Server adapters"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('libibverbs')
source=("http://www.openfabrics.org/downloads/nes/${pkgname}-${pkgver}.tar.gz")
md5sums=('c51afea009ad43924db971a3b9f0df28')
sha512sums=('2b34eb5ef65c225a4f48d9967ccfa5730d7a0cb3e7eaa9e8a2adfb43c6654668a83fc82dd0dec0e93bc760522fb0ae387686c9a094a8342a02c3f9308d3bd25e')

build() {
  cd "${srcdir}/${pkgname}"
  ./autogen.sh
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
