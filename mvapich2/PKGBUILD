# Contributor: lishengming <lishengming.zju@gmail.com>
# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs
pkgname=mvapich2
pkgver=2.0b
pkgrel=2
pkgdesc="A MPI over InfiniBand, 10GigE/iWARP and RoCE"
url="http://mvapich.cse.ohio-state.edu/"
arch=('i686' 'x86_64')
license=("BSD")
depends=(
    'gcc-fortran'
    'libxml2'
    'libibumad'
    'libibmad'
    'libibverbs'
    'hwloc'
)
conflicts=('mpich2')
provides=('mpich2')
source=(
    "http://mvapich.cse.ohio-state.edu/download/mvapich2/${pkgname}-${pkgver}.tgz"
    "mvapich2.profile"
)
# cksum start
md5sums=('69b21b9058e54f20ac117b18f2b7072a'
         '723bbf1aa54da5bab7aa09edfcae9116')
sha512sums=('f7842be383c5a8a989f15c24248e100c4b58bf1c403a00a75f79f2ee079f855c67e9a5f2225cafcd1ab1d12f89d46c93891331ba957065d60322d9a7235018e5'
            'e25f8be4196e57ad095127be82648f14c335aa550ce26c71a8270df10a763902392c8459e19dc9243ded48565beb0f4d0ba9b827d5353a0fd25b86633e31ac5d')
# cksum end

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p build
  cd build

  # easy way, see more here:
  # http://mvapich.cse.ohio-state.edu/support/user_guide_mvapich2-1.8_alpha2.html
  ../configure \
      --prefix=/opt/mvapich2 \
      --enable-shared \
      --enable-sharedlibs=gcc \
      --with-ib-libpath=/usr/lib \
      --with-hwloc \
      --enable-g=all \
      --enable-error-messages=all
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make -j1 DESTDIR="${pkgdir}" install

  install -D -m755 "${srcdir}/${pkgname}.profile" "${pkgdir}/etc/profile.d/mvapich2.sh"
  mkdir -p "${pkgdir}/etc/ld.so.conf.d"
  echo "/opt/mvapich2/lib" > "${pkgdir}/etc/ld.so.conf.d/mvapich2.conf"

}
