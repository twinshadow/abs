# Contributor: lishengming <lishengming.zju@gmail.com>
# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs
pkgname=mvapich2
pkgver=2.0a
pkgrel=1
pkgdesc="A MPI over InfiniBand, 10GigE/iWARP and RoCE"
url="http://mvapich.cse.ohio-state.edu/"
arch=('i686' 'x86_64')
license=("BSD")
depends=(
    'gcc-fortran'
    'libxml2'
    'libibumad'
    'libibmad'
    'libibverbs')
conflicts=('mpich2')
provides=('mpich2')
source=(
    "http://mvapich.cse.ohio-state.edu/download/mvapich2/mv2/${pkgname}-${pkgver}.tgz"
    "mvapich2.profile")
# cksum start
md5sums=('911c3ac4fefc6452aba4f8aadf0dceb0'
         '723bbf1aa54da5bab7aa09edfcae9116')
sha512sums=('9d10457d978693f70ac31eb67ca1aef6220ec4a93163f93a64d7cb30a9d246a1f87f83baa29ccb16d6745806ee59b92d9b52dedd2515165c018d593068c49f36'
            'e25f8be4196e57ad095127be82648f14c335aa550ce26c71a8270df10a763902392c8459e19dc9243ded48565beb0f4d0ba9b827d5353a0fd25b86633e31ac5d')
# cksum end

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p build
  cd build

  # easy way, see more here:
  # http://mvapich.cse.ohio-state.edu/support/user_guide_mvapich2-1.8_alpha2.html
  ../configure \
      --prefix=/opt/mvapich2 \
      --enable-shared \
      --enable-sharedlibs=gcc \
      --with-ib-libpath=/usr/lib \
      --enable-g=all \
      --enable-error-messages=all
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make -j1 DESTDIR="${pkgdir}" install

  install -D -m755 "${srcdir}/${pkgname}.profile" "${pkgdir}/etc/profile.d/mvapich2.sh"
  mkdir -p "${pkgdir}/etc/ld.so.conf.d"
  echo "/opt/mvapich2/lib" > "${pkgdir}/etc/ld.so.conf.d/mvapich2.conf"

}
