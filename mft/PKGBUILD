# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs
pkgname=mft
pkgver=2.7.1
_pkgrev=a-1
_mftver=7
pkgrel=2
pkgdesc="Mellanox firmware tools"
url="http://www.mellanox.com/"
arch=('x86_64' 'i686')
license=('custom:"Mellanox Proprietary License"')
depends=('gcc-libs' 'bash' 'tcl' 'linux-headers')
makedepends=('rpmextract')
source=("http://www.mellanox.com/downloads/MFT/${pkgname}-${pkgver}${_pkgrev}.tgz")
# cksum start
md5sums=('f01bd689f4ac3168d3df7be5c63eda3e')
sha512sums=('3bf6dfb192f130c9941e8ef44811fd4eaacac1565451edcf4ac02261e7295021274e99e98493f73294bc84092f74ca428b5336546e27a4be0b39db5e8d01fde9')
# cksum end

build() {
  cd "${srcdir}/${pkgname}-${pkgver}${_pkgrev}"
  rpmextract.sh "SRPMS/kernel-mft-${pkgver}-${_mftver}.src.rpm"
  tar xzf "kernel-mft-${pkgver}.tgz"
  cd "kernel-mft-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}${_pkgrev}"
  cd "kernel-mft-${pkgver}"
  mkdir -p "${pkgdir}/lib/modules/$(uname -r)/kernel/drivers/mft"
  cp mst_pciconf.ko "${pkgdir}/lib/modules/$(uname -r)/kernel/drivers/mft/mst_pciconf.o"
  cp mst_pci.ko "${pkgdir}/lib/modules/$(uname -r)/kernel/drivers/mft/mst_pci.o"

  cd "${pkgdir}"
  case "$CARCH" in
    i686)
      rpmextract.sh "${srcdir}/${pkgname}-${pkgver}${_pkgrev}/RPMS/${pkgname}-${pkgver}${_pkgrev}.x86.rpm" ;;
    x86_64)
      rpmextract.sh "${srcdir}/${pkgname}-${pkgver}${_pkgrev}/RPMS/${pkgname}-${pkgver}${_pkgrev}.x86_64.rpm" ;;
    *)
      return 1 ;;
  esac

  cd "${srcdir}/${pkgname}-${pkgver}${_pkgrev}"
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  rm -rf "${pkgdir}/usr/lib/mft" "${pkgdir}/usr/lib64/mft" "${pkgdir}/usr/bin/mst"
  mv "${pkgdir}/etc/init.d" "${pkgdir}/etc/rc.d"
  ln -s "/etc/rc.d/mst" "${pkgdir}/usr/bin/mst"
}
