# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs

_realpkgname="nova"
pkgname="openstack-${_realpkgname}-git"
pkgdesc="OpenStack cloud computing fabric controller"
pkgver=0.0.0
pkgrel=1
conflicts=("openstack-${_realpkgname}" "${_realpkgname}")
provides=("openstack-${_realpkgname}")
arch=("any")
license=("custom")
url="https://github.com/openstack/${_realpkgname}"
depends=(
    "openstack-glance-git"
    "python2-babel"
    "python2-kombu"
    "python2-cheetah"
    "python2-daemon"
    "python2-feedparser"
    "python2-gflags"
    "python2-lockfile"
    "python2-netaddr"
    "python2-paramiko"
    "python2-suds"
)
makedepends=(
    "python2-setuptools"
    "git"
)
source=("$pkgname"::"git+https://github.com/openstack/${_realpkgname}")
md5sums=("SKIP")

pkgver() {
  cd "$pkgname"
  local ver="$(git describe --long)"
  printf "%s" "${ver//-/.}"
}

build() {
  cd "$pkgname"
  python2 setup.py build
}

package() {
  cd "$pkgname"
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -dm600 "$pkgdir/etc/${_realpkgname}/rootwrap.d/"
  install -Dm600 $(find etc/${_realpkgname} -type f -mindpeth 1 -maxdepth 1) "$pkgdir/etc/${_realpkgname}/"
  install -Dm600 etc/${_realpkgname}/rootwrap.d/* "$pkgdir/etc/${_realpkgname}/rootwrap.d/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${_realpkgname}/LICENSE"
}
