# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs
pkgname=ibutils
pkgver=1.5.7
_subver=0.1
_commit=g05a9d1a
pkgrel=6
pkgdesc="Mellanox InfiniBand diagnostic tools"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('opensm' 'perl' 'tcl' 'tk')
optdepends=('graphviz' 'swig')
source=("https://www.openfabrics.org/downloads/${pkgname}/${pkgname}-${pkgver}-${_subver}.${_commit}.tar.gz")
# cksum start
md5sums=('15449ec88ee8f3c2be443531dec2f106')
sha512sums=('fe12bf3a448706a7f66e7994e0852e6603fcce58a77eecfc7815e9240cc4dfea319b8fffd237a7a8aabd4cc3041e04457ae1ef2aecc58bae8d122334062f2f30')
# cksum end

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --with-tcl-lib=/usr/lib/libtcl8.6 \
              --with-tk-lib=/usr/lib/libtk8.6
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/bin/git_version.tcl"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
